plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
    id 'maven-publish'
}

String name = "Insights"
group = 'dev.frankheijden.insights'
String dependencyDir = group + '.dependencies'
version = '6.6.1'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'maven-publish'

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

    ext {
        baseName = name
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.codemc.io/repository/maven-public' }
        maven { url 'https://repo.codemc.io/repository/nms/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
        maven { url 'https://libraries.minecraft.net' }
    }

    dependencies {
        compileOnly 'io.papermc.paper:paper:1.17-R0.1-SNAPSHOT'
        implementation 'com.github.FrankHeijden:MinecraftReflection:123e2f546c'
        implementation 'io.papermc:paperlib:1.0.6'
        implementation 'org.bstats:bstats-bukkit:1.8'
        implementation 'net.kyori:adventure-api:4.8.1'
        implementation 'net.kyori:adventure-platform-bukkit:4.0.0-SNAPSHOT'
    }

    compileJava {
        options.compilerArgs += ["-parameters"]
        options.encoding = "UTF-8"
    }

    test {
        useJUnitPlatform()
    }

    checkstyle {
        toolVersion "8.38"
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")

        ignoreFailures = false
        maxErrors = 0
        maxWarnings = 0
    }

    shadowJar {
        relocate 'dev.frankheijden.minecraftreflection', dependencyDir + '.minecraftreflection'
        relocate 'io.papermc.lib', dependencyDir + '.paperlib'
        relocate 'org.bstats', dependencyDir + '.bstats'
        relocate 'net.kyori.adventure', dependencyDir + '.adventure'
        relocate 'net.kyori.examination', dependencyDir + '.examination'
    }

    shadowJar.dependsOn checkstyleMain, checkstyleTest, test
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(path: ':Insights-API', configuration: 'shadow')
    implementation project(path: ':Insights', configuration: 'shadow')
}

shadowJar {
    archiveFileName = "${name}-${archiveVersion.orNull}.${archiveExtension.orNull}"
}

def outputTasks() {
    ["shadowJar", ":Insights:shadowJar", ":Insights-API:shadowJar"].stream().map({ tasks.findByPath(it) })
}

task copyJars(type: Copy) {
    outputTasks().forEach({
        from(it) {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
        }
    })
    rename '(.*)-all.jar', '$1.jar'
    into file('jars')
}

task cleanJars() {
    delete file('jars')
}

publishing {
    repositories {
        maven {
            name = "GitHubPackages"
            url = "https://maven.pkg.github.com/InsightsPlugin/Insights"
            credentials {
                username = System.getenv("GITHUB_ACTOR")
                password = System.getenv("GITHUB_TOKEN")
            }
        }
    }
}

clean.dependsOn cleanJars
build.dependsOn shadowJar
build.dependsOn copyJars
build.dependsOn publishToMavenLocal
