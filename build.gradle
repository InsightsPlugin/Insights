plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.0.0'
}

String name = "Insights"
group = 'dev.frankheijden.insights'
String dependencyDir = group + '.dependencies'
version = '6.5.0'

sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

subprojects {
    apply plugin: 'java'
    apply plugin: 'checkstyle'
    apply plugin: 'com.github.johnrengelman.shadow'

    sourceCompatibility = targetCompatibility = JavaVersion.VERSION_16

    ext {
        baseName = name
    }

    repositories {
        mavenCentral()
        maven { url 'https://jitpack.io' }
        maven { url 'https://repo.codemc.io/repository/maven-public' }
        maven { url 'https://repo.codemc.io/repository/nms/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
        maven { url 'https://papermc.io/repo/repository/maven-public/' }
        maven { url 'https://libraries.minecraft.net' }
    }

    dependencies {
        compileOnly 'io.papermc.paper:paper-api:1.17-R0.1-SNAPSHOT'
        compileOnly 'org.spigotmc:spigot:1.17-R0.1-SNAPSHOT'
        implementation 'com.github.FrankHeijden:MinecraftReflection:1.0.0'
        implementation 'io.papermc:paperlib:1.0.6'
        implementation 'org.bstats:bstats-bukkit:1.8'
    }

    compileJava {
        options.compilerArgs += ["-parameters"]
        options.fork = true
        options.forkOptions.executable = 'javac'
    }

    test {
        useJUnitPlatform()
    }

    checkstyle {
        toolVersion "8.38"
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")

        ignoreFailures = false
        maxErrors = 0
        maxWarnings = 0
    }

    shadowJar {
        relocate 'dev.frankheijden.minecraftreflection', dependencyDir + '.minecraftreflection'
        relocate 'io.papermc.lib', dependencyDir + '.paperlib'
        relocate 'org.bstats', dependencyDir + '.bstats'
    }

    shadowJar.dependsOn checkstyleMain, checkstyleTest, test
}

repositories {
    mavenCentral()
}

dependencies {
    implementation project(path: ':Insights-API', configuration: 'shadow')
    implementation project(path: ':Insights', configuration: 'shadow')
}

shadowJar {
    archiveFileName = "${name}-${archiveVersion.orNull}.${archiveExtension.orNull}"
}

def outputTasks() {
    ["shadowJar", ":Insights:shadowJar", ":Insights-API:shadowJar"].stream().map({ tasks.findByPath(it) })
}

task copyJars(type: Copy) {
    outputTasks().forEach({
        from(it) {
            duplicatesStrategy = DuplicatesStrategy.INCLUDE
        }
    })
    rename '(.*)-all.jar', '$1.jar'
    into file('jars')
}

task cleanJars() {
    delete file('jars')
}

clean.dependsOn cleanJars
build.dependsOn shadowJar
build.dependsOn copyJars
